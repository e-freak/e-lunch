defmodule LunchOrder.Users.User do
  use Ecto.Schema
  import Ecto.Changeset

  alias LunchOrder.Repo
  alias LunchOrder.Users.User

  schema "users" do
    field :email, :string
    field :is_admin, :boolean, default: false
    field :name, :string
    field :organization, :string
    field :floor, :integer, default: 6
    field :password, :string, virtual: true
    field :password_hash, :string
    field :user_id, :string

    timestamps()
  end

  # floorの更新
  def changeset_update(user, %{floor: _} = attrs) do
    user
    |> cast(attrs, [:floor])
  end

  def changeset_update(user, attrs) do
    user
    |> cast(attrs, [:user_id, :name, :password, :email, :organization, :is_admin])
    |> validate_changeset
  end

  @doc false
  def changeset(user, attrs) do
    user
    |> cast(attrs, [:user_id, :name, :password, :email, :organization, :is_admin])
    |> validate_required([:user_id, :name, :password, :email, :organization])
    |> validate_changeset
  end

  defp validate_changeset(user) do
    user
    |> validate_length(:email, min: 5, max: 255)
    |> validate_format(:email, ~r/@/)
    |> unique_constraint(:email)
    |> unique_constraint(:user_id)
    |> validate_length(:password, min: 12)
    # |> validate_format(:password, ~r/^(?=.*[a-z])(?=.*[A-Z])(?=.*\d).*/, [message: "Must include at least one lowercase letter, one uppercase letter, and one digit"])
    |> generate_password_hash
  end

  defp generate_password_hash(changeset) do
    case changeset do
      %Ecto.Changeset{valid?: true, changes: %{password: password}} ->
        put_change(changeset, :password_hash, Comeonin.Bcrypt.hashpwsalt(password))
      _ ->
        changeset
    end
  end

  def find_and_confirm_password(email, password) do
    case Repo.get_by(User, email: email) do
      nil ->
        {:error, :not_found}
      user ->
        if Comeonin.Bcrypt.checkpw(password, user.password_hash) do
          {:ok, user}
        else
          {:error, :unauthorized}
        end
    end
  end

end
